# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=${DOTFILES:-$HOME/dotfiles}

# project folder for quick cd helpers
export PROJECTS=~/projects

# keep legacy vars out of the environment
unset ZSH OHMYZSH

# load private environment variables if present
if [[ -f ~/.localrc ]]; then
  source ~/.localrc
fi

# gather all zsh configuration files
setopt null_glob
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)
unsetopt null_glob

# load the path files first
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source "$file"
done

# initialize completion system before loading custom completion tweaks
autoload -Uz compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

unset config_files

